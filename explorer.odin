package main

import "core:os"
import "core:strings"
import "core:path/filepath"
import win32 "core:sys/windows"
import "ui"

Explorer :: struct {
    rootPath: string,
    items: [dynamic]ExplorerItem,
}

ExplorerItem :: struct {
    name: string,
    fullPath: string,
    isDir: bool,
    isOpen: bool,
    level: i32, // how deep relative to root folder the item is
    child: [dynamic]ExplorerItem,
}

//TODO: since some changes might happen in files structure, we should always load files and compare them with current loaded files!

renderFolderExplorer :: proc() {
    if windowData.explorer == nil { return }

    topOffset: i32 = 25 // TODO: make it configurable
    
    // @(static)
    // explorerWidth: i32 = 200 // TODO: make it configurable

    bgRect := ui.Rect{
        top = windowData.size.y / 2 - topOffset,
        bottom = -windowData.size.y / 2,
        left = -windowData.size.x / 2,
        right = -windowData.size.x / 2 + windowData.explorerWidth,
    }

    ui.pushCommand(&windowData.uiContext, ui.RectCommand{
        rect = bgRect,
        bgColor = GRAY_COLOR,
    })

    // explorer header
    explorerButtonsWidth: i32 = 50 
    explorerHeaderHeight: i32 = 25
    
    headerPosition: int2 = { -windowData.size.x / 2, windowData.size.y / 2 - topOffset - explorerHeaderHeight }
    headerBgRect := ui.toRect(headerPosition, int2{ windowData.explorerWidth, explorerHeaderHeight })

    // header background
    ui.putEmptyElement(&windowData.uiContext, headerBgRect)
    
    ui.pushCommand(&windowData.uiContext, ui.RectCommand{
        rect = headerBgRect,
        bgColor = GRAY_COLOR,
    })

    // explorer root folder name
    // ui.pushCommand(&windowData.uiContext, ui.ClipCommand{
    //     rect = ui.toRect(headerPosition, { windowData.explorerWidth - explorerButtonsWidth, explorerHeaderHeight }), 
    // })
    ui.pushCommand(&windowData.uiContext, ui.TextCommand{
        text = filepath.base(windowData.explorer.rootPath), 
        position = headerPosition,
        color = WHITE_COLOR,
    })
    // ui.pushCommand(&windowData.uiContext, ui.ResetClipCommand{})

    topOffset += explorerHeaderHeight

    @(static)
    topItemIndex: i32 = 0
    
    contentRect := ui.Rect{
        top = windowData.size.y / 2 - topOffset,
        bottom = -windowData.size.y / 2,
        left = -windowData.size.x / 2,
        right = -windowData.size.x / 2 + windowData.explorerWidth,
    }
    contentRectSize := ui.getRectSize(contentRect)

    itemVerticalPadding :: 4
    itemHeight := i32(windowData.font.lineHeight) + itemVerticalPadding 

    maxItemsOnScreen := contentRectSize.y / itemHeight

    // explorer action buttons
    // collapse button
    activeTab := getActiveTab()
    if .SUBMIT in ui.renderButton(&windowData.uiContext, ui.ImageButton{
        position = { headerPosition.x + windowData.explorerWidth - explorerButtonsWidth, headerPosition.y },
        size = { explorerHeaderHeight, explorerHeaderHeight },
        textureId = i32(TextureId.COLLAPSE_FILES_ICON),
        texturePadding = 4,
        hoverBgColor = ui.getDarkerColor(GRAY_COLOR),
    }) {
        collapseExplorer(windowData.explorer)
    }

    // jump to current file button
    if activeTab != nil && 
        .SUBMIT in ui.renderButton(&windowData.uiContext, ui.ImageButton{
        position = { headerPosition.x + windowData.explorerWidth - explorerButtonsWidth + explorerHeaderHeight, headerPosition.y },
        size = { explorerHeaderHeight, explorerHeaderHeight },
        textureId = i32(TextureId.JUMP_TO_CURRENT_FILE_ICON),
        texturePadding = 4,
        hoverBgColor = ui.getDarkerColor(GRAY_COLOR),
    }) {
        if expandExplorerToFile(windowData.explorer, activeTab.filePath) {
            itemIndex := getIndexInFlatenItemsByFilePath(activeTab.filePath, &windowData.explorer.items)

            if itemIndex < topItemIndex {
                topItemIndex = itemIndex
            } else if itemIndex >= topItemIndex + maxItemsOnScreen {
                topItemIndex = itemIndex - maxItemsOnScreen + 1
            }
        }
    }

    position: int2 = {
        -windowData.size.x / 2,
        windowData.size.y / 2 - topOffset - i32(windowData.font.lineHeight),
    }

    ui.beginScroll(&windowData.uiContext)

    openedItems := make([dynamic]^ExplorerItem)
    getOpenedItemsFlaten(&windowData.explorer.items, &openedItems)
    defer delete(openedItems)
    openedItemsCount := i32(len(openedItems))

    @(static)
    itemsLeftOffset: i32 = 0

    topItemIndex = min(topItemIndex, openedItemsCount - maxItemsOnScreen)
    topItemIndex = max(topItemIndex, 0)
    lastItemIndex := min(openedItemsCount, maxItemsOnScreen + topItemIndex)
    maxWidthItem: i32 = 0

    @(static)
    showFileContextMenu := false

    // TODO: it's possible to have a bug if selected got deleted externally
    @(static)
    itemContextMenuIndex: i32 = -1

    @(static)
    renameItemIndex: i32 = -1

    @(static)
    fileContextMenuPosition: int2 = {-1,-1}
    fileContextMenuSize: int2 = { 130, 100 }

    // TODO: used only for setting focus to just selected item's action (like rename). that looks awful!
    @(static)
    fileContextMenuJustOpened := false
    for itemIndex in topItemIndex..<lastItemIndex {
        item := openedItems[itemIndex]
        defer position.y -= itemHeight

        itemRect := ui.Rect{ 
            top = position.y + itemHeight, 
            bottom = position.y,
            left = position.x,
            right = position.x + windowData.explorerWidth,
        }

        if renameItemIndex == itemIndex {
            if .ESC in inputState.wasPressedKeys {
                renameItemIndex = -1
                continue
            }
            
            textInputActions, textInputId := renderTextField(&windowData.uiContext, ui.TextField{
                text = item.name,
                initSelection = { i32(len(filepath.short_stem(item.name))), 0 },
                position = position,
                size = { windowData.explorerWidth, itemHeight },
            })

            if fileContextMenuJustOpened {
                windowData.uiContext.tmpFocusedId = textInputId
                fileContextMenuJustOpened = false
            }

            if .ENTER in inputState.wasPressedKeys {
                windowData.uiContext.tmpFocusedId = 0
            }

            if .LOST_FOCUS in textInputActions {
                // if after rename the name is the same, do nothing
                newFileName := strings.to_string(windowData.uiTextInputCtx.text)

                if newFileName == item.name {
                    renameItemIndex = -1
                    continue
                }

                newFilePath := strings.builder_make(context.temp_allocator)

                strings.write_string(&newFilePath, filepath.dir(item.fullPath))
                strings.write_rune(&newFilePath, filepath.SEPARATOR)
                strings.write_string(&newFilePath, newFileName)

                if len(newFileName) == 0 {
                    fileContextMenuJustOpened = true

                    ui.pushAlert(&windowData.uiContext, ui.Alert{
                        text = strings.clone("Can't save empty file name!"),
                        timeout = 5.0,
                        bgColor = RED_COLOR,
                    })
                    continue
                }

                err := os.rename(item.fullPath, strings.to_string(newFilePath))

                if err != nil {
                    fileContextMenuJustOpened = true

                    ui.pushAlert(&windowData.uiContext, ui.Alert{
                        text = strings.clone(fmt.tprintf("Error: %s!", err)),
                        timeout = 5.0,
                        bgColor = RED_COLOR,
                    })
                    continue
                }
                
                // update into in tab
                tabIndex := getFileTabIndex(windowData.fileTabs[:], item.fullPath)
                if tabIndex != -1 {
                    tab := &windowData.fileTabs[tabIndex]

                    delete(tab.filePath)
                    delete(tab.name)

                    tab.filePath = strings.clone(strings.to_string(newFilePath))
                    tab.name = strings.clone(newFileName)
                }

                // update explorer
                validateExplorerItems(&windowData.explorer)

                renameItemIndex = -1
            }
            continue
        }

        itemActions, _ := ui.putEmptyElement(&windowData.uiContext, itemRect, customId = itemIndex)

        if activeTab != nil && item.fullPath == activeTab.filePath { // highlight selected file
            ui.pushCommand(&windowData.uiContext, ui.RectCommand{
                rect = itemRect,
                bgColor = ui.getDarkerColor(GRAY_COLOR),
            })
        }

        if .FOCUSED in itemActions && .F2 in inputState.wasPressedKeys {
            renameItemIndex = itemIndex
            fileContextMenuJustOpened = true
        }

        if .HOT in itemActions {
            ui.pushCommand(&windowData.uiContext, ui.RectCommand{
                rect = itemRect,
                bgColor = THEME_COLOR_1,
            })
        }

        if .SUBMIT in itemActions {
            if item.isDir {
                item.isOpen = !item.isOpen

                if item.isOpen {
                    populateExplorerSubItems(item.fullPath, &item.child, item.level + 1)
                } else {
                    removeExplorerSubItems(item)
                }
            } else {
                loadFileIntoNewTab(item.fullPath)
            }
        }

        if .RIGHT_CLICK in itemActions {
            showFileContextMenu = true
            fileContextMenuPosition = ui.screenToDirectXCoords(inputState.mousePosition, &windowData.uiContext)

            //fileContextMenuPosition, _ := ui.fitRectOnWindow(fileContextMenuPosition, fileContextMenuSize, &windowData.uiContext)

            itemContextMenuIndex = itemIndex
        }

        // TODO: LOST_FOCUS triggers to soon
        // if .LOST_FOCUS in itemActions {
        //     itemContextMenuIndex = -1
        // }
        
        // ui.pushCommand(&windowData.uiContext, ui.ClipCommand{
        //     rect = itemRect,
        // })

        iconSize: i32 = 16
        icon: TextureId

        if item.isDir {
            icon = item.isOpen ? .ARROW_DOWN_ICON : .ARROW_RIGHT_ICON
        } else {
            icon = getIconByFilePath(item.fullPath)
        }

        leftOffset: i32 = itemsLeftOffset + item.level * 20
        itemWidth := iconSize + 5 + i32(getTextWidth(item.name, &windowData.font))
        if itemWidth > maxWidthItem { maxWidthItem = itemWidth } 

        ui.pushCommand(&windowData.uiContext, ui.ImageCommand{
            rect = ui.toRect(int2{ position.x + leftOffset, position.y + itemVerticalPadding / 2 }, int2{ iconSize, iconSize }),
            textureId = i32(icon),
        })
        ui.pushCommand(&windowData.uiContext, ui.TextCommand{
            text = item.name,
            position = { position.x + leftOffset + iconSize + 5, position.y + itemVerticalPadding / 2 },
            color = WHITE_COLOR,
            maxWidth = ui.getRectSize(itemRect).x - iconSize - 5 - leftOffset,
        })

        if itemContextMenuIndex == itemIndex {
            ui.pushCommand(&windowData.uiContext, ui.BorderRectCommand{
                rect = ui.toRect(int2{ position.x + leftOffset, position.y }, int2{ maxWidthItem, itemHeight }),
                color = RED_COLOR,
                thikness = 1,
            })
        }
        // ui.pushCommand(&windowData.uiContext, ui.ResetClipCommand{})
    }
    // ui.advanceZIndex(&windowData.uiContext) // there's no need to update zIndex multiple times per explorer item, so we do it once

    verticalScrollSize: i32 = min(i32(f32(contentRectSize.y) * f32(maxItemsOnScreen) / f32(openedItemsCount)), contentRectSize.y)
    horizontalScrollSize: i32 = min(i32(f32(contentRectSize.x) * f32(contentRectSize.x) / f32(maxWidthItem)), contentRectSize.x)
    
    @(static)
    scrollVerticalOffset: i32 = 0

    @(static)
    scrollHorizontalOffset: i32 = 0
    verticalScrollActions, _ := ui.endScroll(&windowData.uiContext, ui.Scroll{
        bgRect = ui.Rect{
            top = contentRect.top,
            bottom = contentRect.bottom,
            right = contentRect.right,
            left = contentRect.right - 15,
        },
        offset = &scrollVerticalOffset,
        size = verticalScrollSize,
        color = ui.setColorAlpha(DARKER_GRAY_COLOR, 0.6),
        hoverColor = ui.setColorAlpha(DARK_GRAY_COLOR, 0.9),
    }, ui.Scroll{
        bgRect = ui.Rect{
            top = contentRect.bottom + 15,
            bottom = contentRect.bottom,
            right = contentRect.right,
            left = contentRect.left,
        },
        offset = &scrollHorizontalOffset,
        size = horizontalScrollSize,
        color = ui.setColorAlpha(DARKER_GRAY_COLOR, 0.6),
        hoverColor = ui.setColorAlpha(DARK_GRAY_COLOR, 0.9),
    })

    if openedItemsCount > maxItemsOnScreen { // has vertical scrollbar
        if .MOUSE_WHEEL_SCROLL in verticalScrollActions || .ACTIVE in verticalScrollActions {
            topItemIndex = i32(f32(openedItemsCount - maxItemsOnScreen) * f32(scrollVerticalOffset) / f32(contentRectSize.y - verticalScrollSize))
        } else {
            scrollVerticalOffset = i32(f32(topItemIndex) * f32(contentRectSize.y - verticalScrollSize) / f32(openedItemsCount - maxItemsOnScreen))
        }
    }

    if maxWidthItem > contentRectSize.x { // has horizontal scrollbar
        itemsLeftOffset = -i32(f32(maxWidthItem - contentRectSize.x) * f32(scrollHorizontalOffset) / f32(contentRectSize.x - horizontalScrollSize))
    }

    // NOTE: it won't work if some code above will show for example a popup
    resizeDirection := ui.putResizableRect(&windowData.uiContext, bgRect)

    #partial switch resizeDirection {
    case .RIGHT:
        windowData.explorerWidth = inputState.mousePosition.x
        windowData.editorPadding.left = windowData.explorerWidth + 50 // TODO: looks awful!!!
        recalculateFileTabsContextRects()
    }

    if ui.beginPopup(&windowData.uiContext, ui.Popup{
        position = fileContextMenuPosition, size = fileContextMenuSize,
        bgColor = DARKER_GRAY_COLOR,
        isOpen = &showFileContextMenu,
        clipRect = ui.Rect{
            top = windowData.size.y / 2 - 25, // TODO: remove hardcoded value
            bottom = -windowData.size.y / 2,
            right = windowData.size.x / 2,
            left = -windowData.size.x / 2,
        },
    }) {
        defer ui.endPopup(&windowData.uiContext)

        if .SUBMIT in ui.renderButton(&windowData.uiContext, ui.TextButton{
            text = "New file",
            position = { 0, 75 },
            size = { 130, 25 },
            noBorder = true,
            hoverBgColor = THEME_COLOR_1,
        }) {
            item := openedItems[itemContextMenuIndex]
            showFileContextMenu = false

            newFilePath := strings.builder_make(context.temp_allocator)

            // NOTE: try to create "New File (n).txt" 
            newFileNumber := 0
            for {
                newFileName := "New File"

                strings.write_string(&newFilePath, item.isDir ? item.fullPath : filepath.dir(item.fullPath))
                strings.write_rune(&newFilePath, filepath.SEPARATOR)
                strings.write_string(&newFilePath, newFileName)
                if newFileNumber > 0 {
                    strings.write_string(&newFilePath, fmt.tprintf(" (%i)", newFileNumber))
                }
                strings.write_string(&newFilePath, ".txt")
    
                if !os.exists(strings.to_string(newFilePath)) {
                    break
                }
                newFileNumber += 1
                strings.builder_reset(&newFilePath)
            }                
            
            err := os.write_entire_file_or_err(strings.to_string(newFilePath), []byte{})
            assert(err == nil)

            loadFileIntoNewTab(strings.to_string(newFilePath))
            expandExplorerToFile(windowData.explorer, strings.to_string(newFilePath))
        }
        
        // if .SUBMIT in renderButton(&windowData.uiContext, UiTextButton{
        //     text = "New folder",
        //     position = { 0, 50 },
        //     size = { 100, 25 },
        //     noBorder = true,
        //     hoverBgColor = THEME_COLOR_1,
        // }) {
        //     item := openedItems[itemContextMenuIndex]
        //     showFileContextMenu = false
        // }

        if .SUBMIT in ui.renderButton(&windowData.uiContext, ui.TextButton{
            text = "Rename",
            position = { 0, 50 },
            size = { 130, 25 },
            noBorder = true,
            hoverBgColor = THEME_COLOR_1,
        }) {
            renameItemIndex = itemContextMenuIndex
            showFileContextMenu = false
            fileContextMenuJustOpened = true
        }

        if .SUBMIT in ui.renderButton(&windowData.uiContext, ui.TextButton{
            text = "Open In Folder",
            position = { 0, 25 },
            size = { 130, 25 },
            noBorder = true,
            hoverBgColor = THEME_COLOR_1,
        }) {
            item := openedItems[itemContextMenuIndex]

            pidl := win32.ILCreateFromPathW(win32.utf8_to_wstring(item.fullPath))
            defer win32.CoTaskMemFree(pidl)
            assert(pidl != nil)

            hr := win32.SHOpenFolderAndSelectItems(pidl, 0, nil, 0)
            assert(hr == 0)

            showFileContextMenu = false
        }

        if .SUBMIT in ui.renderButton(&windowData.uiContext, ui.TextButton{
            text = "Delete",
            position = { 0, 0 },
            size = { 130, 25 },
            noBorder = true,
            hoverBgColor = THEME_COLOR_1,
        }) {
            item := openedItems[itemContextMenuIndex]

            // TODO: add switching to tab, if file is about to be deleted (now the ui thread is blocked by win message)
            // tabIndex := getFileTabIndex(windowData.fileTabs[:], item.fullPath)

            // if tabIndex != -1 {
            //     windowData.activeTabIndex = tabIndex
            // }

            #partial switch showOsConfirmMessage("Edi the editor", fmt.tprintf("Do you really want to delete: \"%s\"?", item.name)) {
            case .YES:
                tabIndex := getFileTabIndex(windowData.fileTabs[:], item.fullPath)

                if tabIndex != -1 {
                    tryCloseFileTab(tabIndex, true)
                }

                err := os.remove(item.fullPath)

                if err != nil {
                    ui.pushAlert(&windowData.uiContext, ui.Alert{
                        text = strings.clone(fmt.tprintf("Couldn't delete: \"%s\"!", item.name)),
                        timeout = 5.0,
                        bgColor = RED_COLOR,
                    })
                    break
                }

                ui.pushAlert(&windowData.uiContext, ui.Alert{
                    text = strings.clone(fmt.tprintf("File: \"%s\" deleted!", item.name)),
                    timeout = 5.0,
                    bgColor = GREEN_COLOR,
                })

                // update explorer
                validateExplorerItems(&windowData.explorer)
            }
            showFileContextMenu = false
        }
    }
}

showExplorer :: proc(root: string) {
    if windowData.explorer != nil {
        clearExplorer(windowData.explorer)
        windowData.explorer = nil
    }

    windowData.explorer = initExplorer(root)

    windowData.editorPadding.left = 250 // TODO: looks awful!!!
    recalculateFileTabsContextRects()
}

@(private="file")
initExplorer :: proc(root: string) -> ^Explorer {
    explorer := new(Explorer)

    explorer.rootPath = root
    populateExplorerSubItems(explorer.rootPath, &explorer.items)

    return explorer
}

populateExplorerSubItems :: proc(root: string, explorerItems: ^[dynamic]ExplorerItem, level: i32 = 0) {
    info, err := os.lstat(root, context.temp_allocator)
    assert(err == nil)
	defer os.file_info_delete(info, context.temp_allocator)

    dirHandler, dirHandlerErr := os.open(info.fullpath, os.O_RDONLY)
    assert(dirHandlerErr == nil)
	defer os.close(dirHandler)
	dirItems, _ := os.read_dir(dirHandler, -1, context.temp_allocator)

    // NOTE: First we populate directory items, then files items
    for item in dirItems {
        if !item.is_dir { continue }

        subItem := ExplorerItem{
            name = strings.clone(item.name),
            fullPath = strings.clone(item.fullpath),
            isDir = true,
            level = level,
        }
        
        // keep it if you want preload all nested folders
        //populateExplorerSubItems(item.fullpath, &subItem.child)

        append(explorerItems, subItem)
    }

    for item in dirItems {
        if item.is_dir { continue }

        subItem := ExplorerItem{
            name = strings.clone(item.name),
            fullPath = strings.clone(item.fullpath),
            isDir = false,
            level = level,
        }
        
        append(explorerItems, subItem)
    }
}

validateExplorerItems :: proc(explorer: ^^Explorer) {
    originalExplorer := explorer^

    if originalExplorer == nil { return }

    updatedExplorer := initExplorer(strings.clone(originalExplorer.rootPath))

    originalExplorerItems := make([dynamic]^ExplorerItem)
    defer delete(originalExplorerItems)
    getOpenedItemsFlaten(&originalExplorer.items, &originalExplorerItems)

    expandOpenedFolders :: proc(itemsToExpand: [dynamic]ExplorerItem, originalItems: [dynamic]^ExplorerItem) {
        for &item in itemsToExpand {
            for originalItem in originalItems {
                if item.isDir && item.fullPath == originalItem.fullPath && originalItem.isOpen {
                    item.isOpen = true
                    populateExplorerSubItems(item.fullPath, &item.child, originalItem.level + 1)
                    expandOpenedFolders(item.child, originalItems)
                    break
                }
            }
        }
    }

    expandOpenedFolders(updatedExplorer.items, originalExplorerItems)

    clearExplorer(explorer^)
    explorer^ = updatedExplorer
}

getOpenedItemsFlaten :: proc(itemsToIterate: ^[dynamic]ExplorerItem, flatenItems: ^[dynamic]^ExplorerItem) {
    for &item in itemsToIterate {
        append(flatenItems, &item)

        if item.isDir && item.isOpen {
            getOpenedItemsFlaten(&item.child, flatenItems)
        }
    }
}

collapseExplorer :: proc(explorer: ^Explorer) {
    for &item in explorer.items {
        if item.isDir {
            removeExplorerSubItems(&item)
            item.isOpen = false
        }
    }
}

expandExplorerToFile :: proc(explorer: ^Explorer, filePath: string) -> bool {
    if len(filePath) == 0 { return false }

    fullElements := strings.split(filePath, "\\")
    defer delete(fullElements)

    isFileInExplorer := false
    rootFolderName := filepath.base(explorer.rootPath)
    elements: []string
    for item, index in fullElements {
        if rootFolderName == item {
            isFileInExplorer = true
            elements = fullElements[index + 1:len(fullElements) - 1]
            break
        }
    }

    if !isFileInExplorer { return false }

    if len(elements) == 0 { return false } // when the file is in root folder

    itemsToExpand := &explorer.items
    for item in elements {
        found := false
        for &explorerItem in itemsToExpand { // TODO: it's better to optimize it
            if item == explorerItem.name {
                found = true
                if !explorerItem.isOpen {
                    populateExplorerSubItems(explorerItem.fullPath, &explorerItem.child, explorerItem.level + 1)
                    explorerItem.isOpen = true
                }
                
                itemsToExpand = &explorerItem.child

                break
            }
        }

        if !found { return false }
    }

    return true
}

getIndexInFlatenItemsByFilePath :: proc(filePath: string, items: ^[dynamic]ExplorerItem) -> i32 {
    openedItems := make([dynamic]^ExplorerItem)
    defer delete(openedItems)

    getOpenedItemsFlaten(items, &openedItems)

    for item, index in openedItems {
        if item.fullPath == filePath {
            return i32(index)
        }
    }

    return -1
}

closeExplorer :: proc() {
    clearExplorer(windowData.explorer)
    windowData.explorer = nil
    
    windowData.editorPadding.left = 50
    recalculateFileTabsContextRects()    
}

clearExplorer :: proc(explorer: ^Explorer) {
    if explorer == nil { return }

    for &item in explorer.items {
        removeExplorerSubItems(&item, false)
    }
    delete(explorer.rootPath)
    delete(explorer.items)
    free(explorer)
}

removeExplorerSubItems :: proc(item: ^ExplorerItem, keepRootChildArray := true) {
    for &subItem in item.child {
        // if subItem.isDir{
            removeExplorerSubItems(&subItem, false)
        // }
    }

    if !keepRootChildArray {
        delete(item.name)
        delete(item.fullPath)
        delete(item.child)
    } else {
        clear(&item.child)
    }
}
